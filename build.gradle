buildscript {
    ext.kotlin_version = '1.1.60'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}

apply plugin: 'maven-publish'
apply plugin: 'kotlin'

group 'com.wenhui'
version '1.0.0'

sourceCompatibility = 1.7

ext {
    bintrayUser = null
    bintrayApiKey = null

    if (hasProperty("BINTRAY_API_USER")) {
        bintrayUser = BINTRAY_API_USER
    }

    if (hasProperty("BINTRAY_API_KEY")) {
        bintrayApiKey = BINTRAY_API_KEY
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compileOnly 'com.google.android:android:4.1.1.4'

    testCompile 'junit:junit:4.12'
    //noinspection GradleDependency
    testCompile 'org.assertj:assertj-core:2.8.0'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'
    testCompile 'org.mockito:mockito-core:2.7.22'
}


//required for jcenter
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//required for jcenter
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar //required for jcenter
    archives javadocJar //required for jcenter
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId group
            artifactId 'prioritizeretrofitadapter'
            version project.version
            artifact sourcesJar {
                classifier = 'sources'
            }
            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}

// To upload a new version
// ./gradlew clean
// ./gradlew build bintrayUpload
bintray {
    if (bintrayUser == null || bintrayApiKey == null) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        bintrayUser = properties.getProperty("bintray.user")
        bintrayApiKey = properties.getProperty("bintray.apikey")
    }

    user = bintrayUser
    key = bintrayApiKey
    publications = ['MyPublication']
    pkg {
        repo = 'snapshot'
        name = 'prioritizedretrofitadapter'
        desc = 'Adapt Retrofit Call to support request priority'
        licenses = ["Apache-2.0"]
        websiteUrl = "https://github.com/wenhuiyao/PrioritizeRetrofit"
        vcsUrl = "https://github.com/wenhuiyao/PrioritizeRetrofit"
        issueTrackerUrl = "https://github.com/wenhuiyao/PrioritizeRetrofit/issues"
        publish = true
        version {
            name = project.version
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}